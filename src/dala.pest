dala = _{ SOI ~ functions* ~ eoi }

// Ignore EOI (https://github.com/pest-parser/pest/issues/304)
eoi = _{ !ANY }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)?
}

boolean = { "TRUE" | "FALSE" }

literals = _{ string | number | boolean }

// Functions
functions = _{ upper | concat | sum }

arg  = _{ literals | functions }
args = _{ arg ~ ("," ~ arg)* }

num_arg  = _{ number | functions }
num_args = _{ num_arg ~ ("," ~ num_arg)* }

upper  = { "UPPER" ~ "(" ~ arg ~ ")" }
concat = { "CONCAT" ~ "(" ~ args ~ ")" }
sum    = { "SUM" ~ "(" ~ num_args ~ ")" }
