DALA = _{ SOI ~ FUNCTIONS* ~ eoi }

// Ignore EOI (https://github.com/pest-parser/pest/issues/304)
eoi = _{ !ANY }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

STRING = ${ "\"" ~ INNER ~ "\"" }
INNER  = @{ CHAR* }
CHAR   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

NUMBER = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)?
}

BOOLEAN = { "TRUE" | "FALSE" }

LITERALS = _{ STRING | NUMBER | BOOLEAN }

// Functions
FUNCTIONS = _{ CONCAT | DIVIDE | MULTIPLY | SUBTRACT | SUM | UPPER }

ARG  = _{ LITERALS | FUNCTIONS }
ARGS = _{ ARG ~ ("," ~ ARG)* }

NUM_ARG  = _{ NUMBER | FUNCTIONS }
NUM_ARGS = _{ NUM_ARG ~ ("," ~ NUM_ARG)* }

CONCAT   = { "CONCAT" ~ "(" ~ ARGS ~ ")" }
DIVIDE   = { "DIVIDE" ~ "(" ~ NUM_ARG ~ "," ~ NUM_ARG ~ ")" }
MULTIPLY = { "MULTIPLY" ~ "(" ~ NUM_ARG ~ "," ~ NUM_ARG ~ ")" }
SUBTRACT = { "SUBTRACT" ~ "(" ~ NUM_ARG ~ "," ~ NUM_ARG ~ ")" }
SUM      = { "SUM" ~ "(" ~ NUM_ARGS ~ ")" }
UPPER    = { "UPPER" ~ "(" ~ ARG ~ ")" }
